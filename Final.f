
HEX

: DELAY BEGIN 1 - DUP 0 = UNTIL DROP ;

3F000000 CONSTANT BASE

BASE 200000 + CONSTANT GPFSEL0
BASE 200004 + CONSTANT GPFSEL1
BASE 200008 + CONSTANT GPFSEL2

BASE 20001C + CONSTANT GPSET0
BASE 200028 + CONSTANT GPCLR0

1 CONSTANT OUTPUT
4 CONSTANT ALT0

DECIMAL

: MASK DECIMAL SWAP 10 MOD DUP 0> IF BEGIN SWAP 3 LSHIFT SWAP 1 - DUP 0= UNTIL THEN DROP ;
: OUT OUTPUT MASK ;
: AF0 ALT0 MASK ;
: SELECT DUP ROT SWAP @ OR SWAP ! ; 

: GPIO2_ALT0 2 AF0 GPFSEL0 SELECT ;
: GPIO3_ALT0 3 AF0 GPFSEL0 SELECT ;
: GPIO18_OUT 18 OUT GPFSEL1 SELECT ;
: GPIO23_OUT 23 OUT GPFSEL2 SELECT ;
: GPIO24_OUT 24 OUT GPFSEL2 SELECT ;
: GPIO26_OUT 26 OUT GPFSEL2 SELECT ;

: BSC1_SET 
	GPIO2_ALT0 
	GPIO3_ALT0 ;

: LEDS_SET 
	GPIO18_OUT 
	GPIO23_OUT 
	GPIO24_OUT ;

: ACOUSTIC_SET 
	GPIO26_OUT ;

HEX

40000 CONSTANT BLUE
800000 CONSTANT GREEN
1000000 CONSTANT RED
4000000 CONSTANT ACOUSTIC

: SWITCH GPSET0 GPCLR0 ;
: ON DROP ! ;
: OFF SWAP DROP ! ;
: BLINK DUP SWITCH ON 9000 DELAY SWITCH OFF 9000 DELAY ;
: WARNING_BLINK RED ACOUSTIC OVER OVER SWITCH ON SWITCH ON 9000 DELAY SWITCH OFF SWITCH OFF 10000 DELAY ;

: CLEAN_LED 
	RED SWITCH OFF 
	ACOUSTIC SWITCH OFF 
	GREEN SWITCH ON ;

: WARNING_LED 
	GREEN SWITCH OFF 
	WARNING_BLINK ;

3F804000 CONSTANT BSC1

BSC1 0 + CONSTANT C_R
BSC1 4 + CONSTANT SR
BSC1 8 + CONSTANT DLR
BSC1 C + CONSTANT SAR
BSC1 10 + CONSTANT FIFO

48 CONSTANT ADC_ADDR
00 CONSTANT CONVERT_R
01 CONSTANT CONFIG_R
44 CONSTANT MSB_CONFIG
83 CONSTANT LSB_CONFIG

: I2C_ON 8000 C_R @ OR C_R ! ;

: SET_READ 1 C_R @ OR C_R ! ;

: SET_WRITE 0 C_R @ OR C_R ! ;

: CLEAR_FIFO 10 C_R @ OR C_R ! ;

: WRITE_FIFO FIFO ! ;

: READ_FIFO FIFO @ ;

: RESET 302 SR @ OR SR ! ;

: DATA_LEN DLR @ OR DLR ! ;

: SLAVE_ADDR SAR @ OR SAR ! ;

: SEND 80 C_R @ OR C_R ! ;

: STATUS SR @ .S ;

: ?FIFO_EMPTY 51 AND 51 = IF 0 THEN ;

: SETUP_I2C 
	DATA_LEN 
	RESET 
	CLEAR_FIFO 
	ADC_ADDR SLAVE_ADDR ;

: CHECK_EMPTY 
	BEGIN SR @ ?FIFO_EMPTY 0= UNTIL ;

: COMPLETE 
	BEGIN SR @ 52 AND 52 = UNTIL ;

: CONFIG_ADC 
	3 SETUP_I2C SET_WRITE CONFIG_R WRITE_FIFO SEND 
	CHECK_EMPTY MSB_CONFIG WRITE_FIFO 
	CHECK_EMPTY LSB_CONFIG WRITE_FIFO ;

: ACCESS_ADC 
	1 SETUP_I2C SET_WRITE CONVERT_R WRITE_FIFO SEND ;

: READ_ADC 
	2 SETUP_I2C SET_READ SEND ;

: ADC_SETUP 
	I2C_ON 
	CONFIG_ADC COMPLETE 
	ACCESS_ADC COMPLETE ;

: READ 
	READ_ADC 100 DELAY READ_FIFO 100 DELAY READ_FIFO ;

: VALUE_ADC 
	SWAP 8 LSHIFT SWAP OR ;

VARIABLE R0_AIR
VARIABLE WARNING
VARIABLE ALLERT 6000 ALLERT  ! 
VARIABLE SUM 0 SUM !

DECIMAL

5000 CONSTANT RL
1620 CONSTANT LPG_TRESHOLD
2048 CONSTANT VCC

: RS DUP VCC SWAP - SWAP / RL * ;

: R0 9830 / R0_AIR ! ;

: RS1 R0_AIR @ * ;

: VOLT_LIMIT RL + VCC RL * SWAP / ;

HEX

: VOLT>DC 4 LSHIFT ;

: DC>VOLT 4 RSHIFT ;

: MEAN 1 BEGIN SWAP SUM +! 1 + DUP 64 > UNTIL 1 - SUM @ SWAP / ;

: SAMPLES 0 BEGIN BLUE BLINK READ VALUE_ADC SWAP 1 + .S CR DUP 64 = UNTIL DROP ;

: SET_AIR 
    SAMPLES 
    MEAN .S 
    DC>VOLT .S 
    DECIMAL RS R0 ;

: SET_WARNING 
    DECIMAL 
    LPG_TRESHOLD 
    RS1 
    VOLT_LIMIT 
    HEX VOLT>DC 
    WARNING ! ;

: CALIBRATION 
    SET_AIR 
    SET_WARNING ;

: ?CLEAN DUP WARNING @ < IF CLEAN_LED THEN ;

: ?WARNING DUP DUP WARNING @ > IF WARNING_LED THEN DROP ;

: SCANNER 
    BEGIN READ VALUE_ADC DC>VOLT DECIMAL .S HEX VOLT>DC ?WARNING ?CLEAN DROP CR 0 UNTIL ;

: SETUP_DETECTOR 
	DECIMAL 
	BSC1_SET 
	LEDS_SET 
	ACOUSTIC_SET
	HEX 
	ADC_SETUP ;

: START 
	CALIBRATION 
	SCANNER ;
